Import("platform")
Import("env")
Import("build_dir")
Import("libilutil")
Import("libilcommon")
Import("libilmath")
Import("libilgraphics")

demoenv = env.Clone()

main_obj = demoenv.Object(target = build_dir + "/main.o",
                          source = "main.c")

def build_demo(name):
    src_dir = "#demos/" + name
    inputs = "%s/*.c %s/*.cpp" % (name, name)
    demo = demoenv.Clone()
    demo.Append(CPPPATH = src_dir, LIBS=['ilutil', 'ilcommon', 'ilmath', 'ilgraphics'])
    if platform == "mingw":
        demo.Append(LIBS=[])
    else:
        demo.Append(LIBS=['dl', 'm'])
    if name == "bouncing-lights":
        demo.ParseConfig("pkg-config bullet --libs --cflags")
    demo.ParseConfig("pkg-config glew --libs --cflags")
    demo.ParseConfig("pkg-config sdl2 --libs --cflags")
    sources = [main_obj]
    for mod in Split(inputs):
        sources.extend(Glob(mod))
    binary = demo.Program(target = build_dir + "/" + name,
                          source = sources)
    Depends(binary, libilutil)
    Depends(binary, libilcommon)
    Depends(binary, libilmath)
    Depends(binary, libilgraphics)

build_demo("noop")
build_demo("box")
build_demo("teapots")

Default(None)
